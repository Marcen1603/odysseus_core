grammar de.uniol.inf.is.odysseus.iql.odl.ODL with de.uniol.inf.is.odysseus.iql.basic.BasicIQL

generate oDL "http://www.uniol.de/inf/is/odysseus/iql/odl/ODL"

import "http://www.uniol.de/inf/is/odysseus/iql/basic/BasicIQL" as basiciql
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvm

ODLModel returns basiciql::IQLModel:
	{ODLModel}
	namespaces+=IQLNamespace*
	(elements+=ODLModelElement)*
;

ODLModelElement returns basiciql::IQLModelElement:
	{ODLModelElement}
	(javametadata+=IQLJavaMetadata)*
	inner=(IQLClass | IQLInterface | ODLOperator)
; 

ODLOperator returns jvm::JvmGenericType:
	{ODLOperator}
	'operator' simpleName=ID ('(' (metadataList=IQLMetadataList)? ')')? '{'
		members += (IQLAttribute | IQLMethod | ODLParameter | ODLMethod | IQLJavaMember)*
	'}'		
;

ODLParameter returns basiciql::IQLAttribute:
	{ODLParameter}		
	(optional?='optional')? parameter?='parameter' ('(' (metadataList=IQLMetadataList)? ')')?
	type=JvmTypeReference simpleName=ID (init=IQLVariableInitialization)? ';'
;


ODLMethod returns basiciql::IQLMethod:
	{ODLMethod}		
	(on?='on' | validate?='validate')
	(simpleName=ID ('(' (parameters+=JvmFormalParameter (',' parameters+=JvmFormalParameter)*)? ')')? (':' returnType=JvmTypeReference)?)?
	body=IQLStatementBlock
;

IQLJavaText:
	(IQL_JAVA_KEYWORDS | WS | ID | BOOLEAN | DOUBLE | STRING | INT | ANY_OTHER |
	'+'  | '+='  | '-'  | '-='  | '*'   | '*='   | '/' | '/=' | '%'  | '%=' |
	'++' | '--'	 | '>'  | '>='  | '<'   | '<='   | '!' | '!=' | '&&' | '||' |
	'==' | '='   | '~'  | '?:'  | '|'   | '|='   | '^' | '^=' | '&'  | '&=' |
	'>>' | '>>=' | '<<' | '<<=' | '>>>' | '>>>=' |
	'['  | ']'   |'{'   |'}'    |  '('  |')'     |
	 '.' | ':'   | ';'  | ','   | 'null' | 
	 'on' | 'validate' | 'parameter' | 'optional' | 'operator')*
;