grammar de.uniol.inf.is.odysseus.iql.qdl.QDL with de.uniol.inf.is.odysseus.iql.basic.BasicIQL

generate qDL "http://www.uniol.de/inf/is/odysseus/iql/qdl/QDL"

import "http://www.uniol.de/inf/is/odysseus/iql/basic/BasicIQL" as iql
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvm

QDLModel returns iql::IQLModel:
	{QDLModel}
	namespaces+=IQLNamespace*
	(elements+=QDLModelElement)*
;

QDLModelElement returns iql::IQLModelElement:
	{QDLModelElement}
	(javametadata+=IQLJavaMetadata)*
	inner=(IQLClass | IQLInterface | QDLQuery)
; 

QDLQuery returns jvm::JvmGenericType:
	{QDLQuery}
	'query' simpleName=ID ('(' (metadataList=IQLMetadataList)? ')')?
	statements=IQLStatementBlock
;

IQLAssignmentExpression returns iql::IQLExpression:
	IQLSubscribeExpression (
		=>({iql::IQLAssignmentExpression.leftOperand=current} op=OpAssign) rightOperand=IQLAssignmentExpression
	)?
;

IQLSubscribeExpression returns iql::IQLExpression:
	IQLPortExpression
	(=>({IQLSubscribeExpression.leftOperand=current} op=IQLSubscribe) rightOperand=IQLPortExpression)*
;

IQLPortExpression returns iql::IQLExpression:
	IQLLogicalOrExpression
	(=>({IQLPortExpression.leftOperand=current} op=':') rightOperand=IQLLogicalOrExpression)*
;


IQLSubscribe:
	'->' | '<-'
;

IQLMetadataValueSingle returns iql::IQLMetadataValue:
	{iql::IQLMetadataValueSingleInt} value = INT|
	{iql::IQLMetadataValueSingleDouble} value = DOUBLE|
	{iql::IQLMetadataValueSingleString} value = STRING|
	{iql::IQLMetadataValueSingleBoolean} value = BOOLEAN|
	(=>({QDLMetadataValueSingleID} value = ID)) |
	{iql::IQLMetadataValueSingleTypeRef} value = JvmTypeReference |	
	{iql::IQLMetadataValueSingleNull} 'null'	
;

IQLJavaWords:
	IQLJavaKeywords | WS | ID | BOOLEAN | DOUBLE | STRING | INT | ANY_OTHER |
	'+'  | '+='  | '-'  | '-='  | '*'   | '*='   | '/' | '/=' | '%'  | '%=' |
	'++' | '--'	 | '>'  | '>='  | '<'   | '<='   | '!' | '!=' | '&&' | '||' |
	'==' | '='   | '~'  | '?:'  | '|'   | '|='   | '^' | '^=' | '&'  | '&=' |
	'>>' | '>>=' | '<<' | '<<=' | '>>>' | '>>>=' |
	'['  | ']'   |'{'   |'}'    |  '('  |')'     |
	 '.' | ':'   | ';'  | ','   | 'null' | 
	 'query'
;

