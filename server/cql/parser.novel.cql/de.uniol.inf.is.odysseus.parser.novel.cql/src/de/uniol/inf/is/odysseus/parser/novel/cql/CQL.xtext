grammar de.uniol.inf.is.odysseus.parser.novel.cql.CQL with org.eclipse.xtext.common.Terminals

generate cQL "http://www.uniol.de/inf/is/odysseus/parser/novel/cql/CQL"

Model: statements+=Statement*;
	
Statement:  (type=Select_Statement | type=Create_Statement | type=StreamTo) (';')? ; 

//Nested_Statement: '(' Select_Statement ')' ;

// Data types //////////////////////////////////////////////////////////

Atomic returns Expression:
//	{NumberConstant} (value=INT | value=INT '.' INT) |
	{IntConstant} value=INT |
	{FloatConstant} value=FLOAT_NUMBER |
	{StringConstant} value= STRING |
	{BoolConstant} value=('TRUE' | 'FALSE') |
	{AttributeRef} value=Attribute//TODO A SELECT * query offers no reference for an attribute.
								  //At the moment is no actual referencing possible!  
;

terminal FLOAT_NUMBER:  INT '.' INT ;

Source: 
	name=ID
	('[' (unbounded=Window_Unbounded|time=Window_Timebased|tuple=Window_Tuplebased)']')?
;

//TODO have to dynamic -> use scope provider
DataType: 
	value=('INTEGER' | 'DOUBLE' | 'FLOAT' | 'STRING' | 'BOOLEAN' 
	| 'STARTTIMESTAMP' | 'ENDTIMESTAMP' 
);

Attribute: name=ID;
 
// Expressions //////////////////////////////////////////////////////////
ExpressionsModel: {ExpressionsModel} elements+=Expression+;

Expression: Or;

Or returns Expression:
	And ({Or.left=current} 'OR' right=And)*
;

And returns Expression:
	Equalitiy ({And.left=current} 'AND' right=Equalitiy)*
;

Equalitiy returns Expression:
	Comparison (
		{Equality.left=current} op=('==' | '!=')
		right=Comparison	
	)*
;

Comparison returns Expression:
	PlusOrMinus (
		{Comparision.left=current} op=('>=' | '<=' | '<' | '>')
		right=PlusOrMinus
	)*
;

PlusOrMinus returns Expression:
	MulOrDiv (
		({Plus.left=current} '+' | {Minus.left=current} '-')
		right=MulOrDiv
	)*
;

MulOrDiv returns Expression:
	Primary (
		{MulOrDiv.left=current} op=('*' | '/')
		right=Primary
	)*
;

Primary returns Expression:
	({Bracket} '(' inner=Expression ')') |
	{NOT} 'NOT' expression=Primary |
	Atomic
;
////////////////////////////////////////////////////////////////////////

Select_Statement:
	name='SELECT'
	('DISTINCT')?
	( '*' | ( (attributes+=Attribute | aggregations+=Aggregation)+ & (',' attributes+=Attribute)* & (',' aggregations+=Aggregation)*))
	'FROM'
	(sources+=Source+ (',' sources+=Source)*)
	('WHERE' predicates=ExpressionsModel)?
	('ORDER' 'BY' order=Attribute)? //ordering+=Attribute+ ('ASC' | 'DESC')?)?
	('HAVING' having=ExpressionsModel)?
//	';'
;

Aggregation:
	name=ID
	'('
	attribute=Attribute
	')'
;

Create_Statement:
	'CREATE' (channelformat=ChannelFormat|accessframework=AccessFramework)
;

AccessFramework:
	type=('STREAM'|'SINK')
	name = ID
	'('
	attributes+= Attribute+ 
	datatypes+= DataType+
	(',' attributes+=Attribute datatypes+= DataType)*
	')'
	'WRAPPER' wrapper=STRING
	'PROTOCOL' protocol=STRING
	'TRANSPORT' transport=STRING
	'DATAHANDLER' datahandler=STRING
	'OPTIONS' 
		'(' 
			(keys+=STRING values+=STRING)+ (',' keys+=STRING values+=STRING)?
		')'
;

ChannelFormat: (stream=ChannelFormatStream|view=ChannelFormatView);

ChannelFormatStream:
	'STREAM'
	name = ID
	'('
	attributes+= Attribute+ 
	datatypes+= DataType+
	(',' attributes+=Attribute datatypes+= DataType)*
	')'
	'CHANNEL'//TODO Add FILE as possible parameter --> and even more ..
	host=ID
	':'
	port=INT
;

ChannelFormatView:
	'VIEW' 
	name=ID
	'FROM'
	'('
	(select=Select_Statement|create=Create_Statement) (';')?
	')'
;

StreamTo:
	'STREAM' 'TO'
	name=ID
	(statement=Select_Statement|inputname=ID)
;

Window_Unbounded:  
	'UNBOUNDED'
;

Window_Timebased: 
	'SIZE' size=INT unit=ID
	('ADVANCE' advance_size=INT advance_unit=ID)?
	'TIME'
;

Window_Tuplebased://TODO PARTITION BY only if a GROUP BY clause exists
	'SIZE' size=INT
	('ADVANCE' advance_size=INT)?
	'TUPLE'
	('PARTITION' 'BY' partition_attribute=Attribute)?
;

Scalar_Function:
	'Scalar'
	name=ID
	';'
;





